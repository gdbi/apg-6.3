;
; Grammar2 - removal of white space (WSP, SWS & LWS) and line ends (CRLF, CR, LF)
;
; SIP MESSAGE
SIP-message       =  BlankSpace (Request / Response) ; forgiving on leading characters
BlankSpace        = *(%d0-31/%d128-255)

; REQUEST
Request           =  Request-Line *( message-header ) CRLF [ message-body ]
Request-Line      =  Method SP Request-URI SP SIP-Version CRLF
SIP-Version       =  "SIP" "/" 1*DIGIT "." 1*DIGIT
INVITEm           =  %x49.4E.56.49.54.45 ; INVITE in caps
ACKm              =  %x41.43.4B ; ACK in caps
OPTIONSm          =  %x4F.50.54.49.4F.4E.53 ; OPTIONS in caps
BYEm              =  %x42.59.45 ; BYE in caps
CANCELm           =  %x43.41.4E.43.45.4C ; CANCEL in caps
REGISTERm         =  %x52.45.47.49.53.54.45.52 ; REGISTER in caps
Method            =  INVITEm / ACKm / OPTIONSm / BYEm / CANCELm / REGISTERm / extension-method
extension-method  =  u_token

; RESPONSE
Response          =  Status-Line *( message-header ) CRLF [ message-body ]
Status-Line       =  SIP-Version SP Status-Code SP Reason-Phrase CRLF
Status-Code       =  3DIGIT ; to be analyzed semantically (see RFC 3621)
Reason-Phrase     =  *(reserved / u_one-or-more-unreserved / escaped / UTF8-NONASCII / UTF8-CONT / (%x20 / %x09))

; SIP URI
SIP-URI           =  "sip:" [ userinfo ] hostport uri-parameters [ headers ]
SIPS-URI          =  "sips:" [ userinfo ] hostport uri-parameters [ headers ]
userinfo          =  ( user / telephone-subscriber ) [ ":" password ] "@"
user              =  1*( u_one-or-more-unreserved / escaped / user-unreserved )
user-unreserved   =  "&" / "=" / "+" / "$" / "," / ";" / "?" / "/"
password          =  *( u_one-or-more-unreserved / escaped / "&" / "=" / "+" / "$" / "," )
hostport          =  host [ ":" port ]
host              =  hostname / IPv4address / IPv6reference
hostname          =  e_domainlabel u_toplabel
port              =  1*DIGIT
uri-parameters    =  *( ";" uri-parameter)
uri-parameter     =  transport-param / user-param / method-param / ttl-param / maddr-param / lr-param / other-param
transport-param   =  "transport=" ( "udp" / "tcp" / "sctp" / "tls" / other-transport)
other-transport   =  u_token
user-param        =  "user=" ( "phone" / "ip" / other-user)
other-user        =  u_token
method-param      =  "method=" Method
ttl-param         =  "ttl=" ttl
maddr-param       =  "maddr=" host
lr-param          =  "lr"
other-param       =  pname [ "=" pvalue ]
pname             =  1*paramchar
pvalue            =  1*paramchar
paramchar         =  param-unreserved / u_unreserved / escaped
param-unreserved  =  "[" / "]" / "/" / ":" / "&" / "+" / "$"
headers           =  "?" header *( "&" header )
header            =  hname "=" hvalue
hname             =  1*( hnv-unreserved / u_one-or-more-unreserved / escaped )
hvalue            =  *( hnv-unreserved / u_one-or-more-unreserved / escaped )
hnv-unreserved    =  "[" / "]" / "/" / "?" / ":" / "+" / "$"

; REQUEST URI
Request-URI    =  SIP-URI / SIPS-URI / absoluteURI
absoluteURI    =  u_scheme ":" ( hier-part / opaque-part )
hier-part      =  ( net-path / abs-path ) [ "?" query ]
net-path       =  "//" authority [ abs-path ]
abs-path       =  "/" path-segments
opaque-part    =  uric-no-slash *uric
uric           =  reserved / u_unreserved / escaped
uric-no-slash  =  u_unreserved / escaped / ";" / "?" / ":" / "@" / "&" / "=" / "+" / "$" / ","
path-segments  =  segment *( "/" segment )
segment        =  *pchar *( ";" param )
param          =  *pchar
pchar          =  u_unreserved / escaped / ":" / "@" / "&" / "=" / "+" / "$" / ","
authority      =  srvr / reg-name
srvr           =  [ [ userinfo ] hostport ]
reg-name       =  1*( u_one-or-more-unreserved / escaped / "$" / "," / ";" / ":" / "@" / "&" / "=" / "+" )
query          =  *uric

; IP ADDRESSES
IPv4address    =  1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT "." 1*3DIGIT
IPv6reference  =  "[" IPv6address "]"
IPv6address    =  *(HEXDIG / ":" / ".") ; to be parsed in semantics

; MESSAGE HEADERS
message-header      = (  "Accept"                   u_HCOLON Accept
                      /  "Accept-Encoding"          u_HCOLON Accept-Encoding
                      /  "Accept-Language"          u_HCOLON Accept-Language
                      /  "Alert-Info"               u_HCOLON Alert-Info
                      /  "Allow"                    u_HCOLON Allow
                      /  "Authentication-Info"      u_HCOLON Authentication-Info
                      /  "Authorization"            u_HCOLON Authorization
                      /  ( "Call-ID" / "i" )        u_HCOLON u_callid
                      /  "Call-Info"                u_HCOLON Call-Info
                      /  ("Contact" / "m" )         u_HCOLON Contact
                      /  "Content-Disposition"      u_HCOLON Content-Disposition
                      /  ("Content-Encoding" / "e") u_HCOLON Content-Encoding
                      /  "Content-Language"         u_HCOLON Content-Language
                      /  ("Content-Length" / "l")   u_HCOLON Content-Length
                      /  ( "Content-Type" / "c" )   u_HCOLON Content-Type
                      /  "CSeq"                     u_HCOLON CSeq
                      /  "Date"                     u_HCOLON Date
                      /  "Error-Info"               u_HCOLON Error-Info
                      /  "Expires"                  u_HCOLON Expires
                      /  ( "From" / "f" )           u_HCOLON From
                      /  "In-Reply-To"              u_HCOLON In-Reply-To
                      /  "Max-Forwards"             u_HCOLON Max-Forwards
                      /  "MIME-Version"             u_HCOLON MIME-Version
                      /  "Min-Expires"              u_HCOLON Min-Expires
                      /  "Organization"             u_HCOLON Organization
                      /  "Priority"                 u_HCOLON Priority
                      /  "Proxy-Authenticate"       u_HCOLON Proxy-Authenticate
                      /  "Proxy-Authorization"      u_HCOLON Proxy-Authorization
                      /  "Proxy-Require"            u_HCOLON Proxy-Require
                      /  "Record-Route"             u_HCOLON Record-Route
                      /  "Reply-To"                 u_HCOLON Reply-To
                      /  "Require"                  u_HCOLON Require
                      /  "Retry-After"              u_HCOLON Retry-After
                      /  "Route"                    u_HCOLON Route
                      /  "Server"                   u_HCOLON Server
                      /  ( "Subject" / "s" )        u_HCOLON Subject
                      /  ( "Supported" / "k" )      u_HCOLON Supported
                      /  "Timestamp"                u_HCOLON Timestamp
                      /  ( "To" / "t" )             u_HCOLON To
                      /  "Unsupported"              u_HCOLON Unsupported
                      /  "User-Agent"               u_HCOLON User-Agent
                      /  ( "Via" / "v" )            u_HCOLON Via
                      /  "Warning"                  u_HCOLON Warning
                      /  "WWW-Authenticate"         u_HCOLON WWW-Authenticate
                      /  header-name                u_HCOLON extension-header) CRLF

Accept              =  [ accept-range *(u_COMMA accept-range) ]
accept-range        =  media-range *(u_SEMI accept-param)
media-range         =  ( "*/*" / ( m-type u_SLASH ("*" / m-subtype) ) ) *( u_SEMI m-parameter )
accept-param        =  ("q" u_EQUAL qvalue) / generic-param
qvalue              =  ( "0" [ "." 0*3DIGIT ] ) / ( "1" [ "." 0*3("0") ] )
generic-param       =  u_token [ u_EQUAL gen-value ]
gen-value           =  u_token / host / quoted-string
Accept-Encoding     =  [ encoding *(u_COMMA encoding) ]
encoding            =  codings *(u_SEMI accept-param)
codings             =  content-coding / "*"
content-coding      =  u_token
Accept-Language     =  [ language *(u_COMMA language) ]
language            =  language-range *(u_SEMI accept-param)
language-range      =  ( ( 1*8ALPHA *( "-" 1*8ALPHA ) ) / "*" )
Alert-Info          =  alert-param *(u_COMMA alert-param)
alert-param         =  u_LAQUOT absoluteURI u_RAQUOT *( u_SEMI generic-param )
Allow               =  [Method *(u_COMMA Method)]
Authorization       =  credentials
credentials         =  ("Digest" u_LWS digest-response) / other-response
digest-response     =  dig-resp *(u_COMMA dig-resp)
dig-resp            =  username / realm / nonce / digest-uri
                      / dresponse / algorithm / cnonce
                      / opaque / message-qop
                      / nonce-count / auth-param
username            =  "username" u_EQUAL username-value
username-value      =  quoted-string
digest-uri          =  "uri" u_EQUAL u_LDQUOT digest-uri-value u_RDQUOT
digest-uri-value    =  *(%x20-21/%x23-FF)
message-qop         =  "qop" u_EQUAL qop-value
cnonce              =  "cnonce" u_EQUAL cnonce-value
cnonce-value        =  nonce-value
nonce-count         =  "nc" u_EQUAL u_nc-value
;nc-value            =  8LHEX
dresponse           =  "response" u_EQUAL u_request-digest
;request-digest      =  u_LDQUOT u_32LHEX u_RDQUOT
auth-param          =  auth-param-name u_EQUAL ( u_token / quoted-string )
auth-param-name     =  u_token
other-response      =  auth-scheme u_LWS auth-param *(u_COMMA auth-param)
auth-scheme         =  u_token
Authentication-Info =  ainfo *(u_COMMA ainfo)
ainfo               =  nextnonce / message-qop / response-auth / cnonce / nonce-count
nextnonce           =  "nextnonce" u_EQUAL nonce-value
response-auth       =  "rspauth" u_EQUAL response-digest
response-digest     =  u_LDQUOT *u_LHEX u_RDQUOT
Call-Info           =  info *(u_COMMA info)
info                =  u_LAQUOT absoluteURI u_RAQUOT *( u_SEMI info-param)
info-param          =  ( "purpose" u_EQUAL ( "icon" / "info" / "card" / u_token ) ) / generic-param
Contact             =  ( u_STAR / (contact-param *(u_COMMA contact-param)))
contact-param       =  (name-addr / addr-spec) *(u_SEMI contact-params)
name-addr           =  display-name u_LAQUOT addr-spec u_RAQUOT
addr-spec           =  SIP-URI / SIPS-URI / absoluteURI
display-name        =  quoted-string / d-name
d-name              =  [u_token *(u_LWS u_token)]
contact-params      =  c-p-q / c-p-expires / contact-extension
c-p-q               =  "q" u_EQUAL qvalue
c-p-expires         =  "expires" u_EQUAL delta-seconds
contact-extension   =  generic-param
delta-seconds       =  1*DIGIT
Content-Disposition =  disp-type *( u_SEMI disp-param )
disp-type           =  "render" / "session" / "icon" / "alert" / disp-extension-token
disp-param          =  handling-param / generic-param
handling-param      =  "handling" u_EQUAL ( "optional" / "required" / other-handling )
other-handling      =  u_token
disp-extension-token=  u_token
Content-Encoding    =  content-coding *(u_COMMA content-coding)
Content-Language    =  language-tag *(u_COMMA language-tag)
language-tag        =  primary-tag *( "-" subtag )
primary-tag         =  1*8ALPHA
subtag              =  1*8ALPHA
Content-Length      =  1*DIGIT
Content-Type        =  media-type
media-type          =  m-type u_SLASH m-subtype *(u_SEMI m-parameter)
m-type              =  discrete-type / composite-type
discrete-type       =  "text" / "image" / "audio" / "video" / "application" / extension-token
composite-type      =  "message" / "multipart" / extension-token
extension-token     =  ietf-token / x-token
ietf-token          =  u_token
x-token             =  "x-" u_token
m-subtype           =  extension-token / iana-token
iana-token          =  u_token
m-parameter         =  m-attribute u_EQUAL m-value
m-attribute         =  u_token
m-value             =  u_token / quoted-string
CSeq                =  1*DIGIT u_LWS Method
Date                =  SIP-date
SIP-date            =  rfc1123-date
rfc1123-date        =  wkday "," SP date1 SP time SP "GMT"
date1               =  2DIGIT SP month SP 4DIGIT ; day month year (e.g., 02 Jun 1982)
time                =  2DIGIT ":" 2DIGIT ":" 2DIGIT ; 00:00:00 - 23:59:59
wkday               =  "Mon" / "Tue" / "Wed" / "Thu" / "Fri" / "Sat" / "Sun"
month               =  "Jan" / "Feb" / "Mar" / "Apr" / "May" / "Jun" / "Jul" / "Aug" / "Sep" / "Oct" / "Nov" / "Dec"
Error-Info          =  error-uri *(u_COMMA error-uri)
error-uri           =  u_LAQUOT absoluteURI u_RAQUOT *( u_SEMI generic-param )
Expires             =  delta-seconds
From                =  from-spec
from-spec           =  ( name-addr / addr-spec ) *( u_SEMI from-param )
from-param          =  tag-param / generic-param
tag-param           =  "tag" u_EQUAL u_token
In-Reply-To         =  u_callid *(u_COMMA u_callid)
Max-Forwards        =  1*DIGIT
MIME-Version        =  1*DIGIT "." 1*DIGIT
Min-Expires         =  delta-seconds
Organization        =  [TEXT-UTF8-TRIM]
Priority            =  priority-value
priority-value      =  "emergency" / "urgent" / "normal" / "non-urgent" / other-priority
other-priority      =  u_token
Proxy-Authenticate  =  challenge
challenge           =  ("Digest" u_LWS digest-cln *(u_COMMA digest-cln)) / other-challenge
other-challenge     =  auth-scheme u_LWS auth-param *(u_COMMA auth-param)
digest-cln          =  realm / domain / nonce / opaque / stale / algorithm / qop-options / auth-param
realm               =  "realm" u_EQUAL realm-value
realm-value         =  quoted-string
domain              =  "domain" u_EQUAL u_LDQUOT URI *( 1*SP URI ) u_RDQUOT
URI                 =  absoluteURI / abs-path
nonce               =  "nonce" u_EQUAL nonce-value
nonce-value         =  quoted-string
opaque              =  "opaque" u_EQUAL quoted-string
stale               =  "stale" u_EQUAL ( "true" / "false" )
algorithm           =  "algorithm" u_EQUAL ( "MD5" / "MD5-sess" / u_token )
qop-options         =  "qop" u_EQUAL u_LDQUOT qop-value *("," qop-value) u_RDQUOT
qop-value           =  "auth" / "auth-int" / u_token
Proxy-Authorization =  credentials
Proxy-Require       =  option-tag *(u_COMMA option-tag)
option-tag          =  u_token
Record-Route        =  rec-route *(u_COMMA rec-route)
rec-route           =  name-addr *( u_SEMI rr-param )
rr-param            =  generic-param
Reply-To            =  rplyto-spec
rplyto-spec         =  ( name-addr / addr-spec ) *( u_SEMI rplyto-param )
rplyto-param        =  generic-param
Require             =  option-tag *(u_COMMA option-tag)
Retry-After         =  delta-seconds [ comment ] *( u_SEMI retry-param )
retry-param         =  ("duration" u_EQUAL delta-seconds) / generic-param
Route               =  route-param *(u_COMMA route-param)
route-param         =  name-addr *( u_SEMI rr-param )
Server              =  server-val *(u_LWS server-val)
server-val          =  product / comment
product             =  u_token [u_SLASH product-version]
product-version     =  u_token
Subject             =  [TEXT-UTF8-TRIM]
Supported           =  [option-tag *(u_COMMA option-tag)]
Timestamp           =  1*(DIGIT) [ "." *(DIGIT) ] [ u_LWS delay ]
delay               =  *(DIGIT) [ "." *(DIGIT) ]
To                  =  ( name-addr / addr-spec ) *( u_SEMI to-param )
to-param            =  tag-param / generic-param
Unsupported         =  option-tag *(u_COMMA option-tag)
User-Agent          =  server-val *(u_LWS server-val)
Via                 =  via-parm *(u_COMMA via-parm)
via-parm            =  sent-protocol u_LWS sent-by *( u_SEMI via-params )
via-params          =  via-ttl / via-maddr / via-received / via-branch / via-extension
via-ttl             =  "ttl" u_EQUAL ttl
via-maddr           =  "maddr" u_EQUAL host
via-received        =  "received" u_EQUAL (IPv4address / IPv6address)
via-branch          =  "branch" u_EQUAL u_token
via-extension       =  generic-param
sent-protocol       =  protocol-name u_SLASH protocol-version u_SLASH transport
protocol-name       =  "SIP" / u_token
protocol-version    =  u_token
transport           =  "UDP" / "TCP" / "TLS" / "SCTP" / other-transport
sent-by             =  host [ u_COLON port ]
ttl                 =  1*3DIGIT ; 0 to 255
Warning             =  warning-value *(u_COMMA warning-value)
warning-value       =  warn-code SP warn-agent SP warn-text
warn-code           =  3DIGIT
warn-agent          =  hostport / pseudonym
warn-text           =  quoted-string
pseudonym           =  u_token
WWW-Authenticate    =  challenge
extension-header    =  header-value
header-name         =  u_token
header-value        =  *(TEXT-UTF8char / UTF8-CONT / u_LWS)
message-body        =  *OCTET

; 2.2 "tel" URL scheme, see [RFC2303]
; NOTE: domain is defined in RFC1035 and is different that the domain defined above
telephone-subscriber  = global-phone-number / local-phone-number
global-phone-number   = "+" base-phone-number [isdn-subaddress] [post-dial] *(area-specifier / service-provider / future-extension)
base-phone-number     = 1*phonedigit
local-phone-number    = 1*(phonedigit / dtmf-digit /
                         pause-character) [isdn-subaddress] [post-dial] area-specifier
                         *(area-specifier / service-provider / future-extension)
isdn-subaddress       = ";isub=" 1*phonedigit
post-dial             = ";postd=" 1*(phonedigit / dtmf-digit / pause-character)
area-specifier        = ";" phone-context-tag "=" phone-context-ident
phone-context-tag     = "phone-context"
phone-context-ident   = network-prefix / private-prefix
network-prefix        = global-network-prefix / local-network-prefix
global-network-prefix = "+" 1*phonedigit
local-network-prefix  = 1*(phonedigit / dtmf-digit / pause-character)
private-prefix        = (%x21-22 / %x24-27 / %x2C / %x2F / %x3A /
                        %x3C-40 / %x45-4F / %x51-56 / %x58-60 /
                        %x65-6F / %x71-76 / %x78-7E)
                        *(%x21-3A / %x3C-7E)
                        ; Characters in URLs must follow escaping rules
                        ; as explained in [RFC2396]
                        ; See sections 1.2 and 2.5.2
service-provider      = ";" provider-tag "=" u_RFC1035domain
;service-provider      = ";" provider-tag "=" provider-hostname
provider-tag          = "tsp"
;provider-hostname     = u_RFC1035domain ; <domain> is defined in [RFC1035] ; See section 2.5.10
future-extension      = ";" 1*(token-char) ["=" ((1*(token-char) ["?" 1*(token-char)]) / tel-quoted-string )]
                        ; See section 2.5.11 and [RFC2543]
token-char            = (%x21 / %x23-27 / %x2A-2B / %x2D-2E / %x30-39 / %x41-5A / %x5E-7A / %x7C / %x7E)
                        ; Characters in URLs must follow escaping rules
                        ; as explained in [RFC2396]
                        ; See sections 1.2 and 2.5.11
tel-quoted-string     = %x22 *( "\" CHAR / (%x20-21 / %x23-7E / %x80-FF )) %x22
                        ; Characters in URLs must follow escaping rules
                        ; as explained in [RFC2396]
                        ; See sections 1.2 and 2.5.11
phonedigit            = DIGIT / visual-separator
visual-separator      = "-" / "." / "(" / ")"
pause-character       = one-second-pause / wait-for-dial-tone
one-second-pause      = "p"
wait-for-dial-tone    = "w"
dtmf-digit            = "*" / "#" / "A" / "B" / "C" / "D"

; 25.1 Basic Rules
reserved        =  ";" / "/" / "?" / ":" / "@" / "&" / "=" / "+" / "$" / ","
escaped         =  "%" HEXDIG HEXDIG
TEXT-UTF8-TRIM  =  1*TEXT-UTF8char *(u_LWS 1*TEXT-UTF8char)
TEXT-UTF8char   =  %x21-7E / UTF8-NONASCII
UTF8-NONASCII   =  %xC0-DF UTF8-CONT /
                   %xE0-EF 2UTF8-CONT /
                   %xF0-F7 3UTF8-CONT /
                   %xF8-Fb 4UTF8-CONT /
                   %xFC-FD 5UTF8-CONT
UTF8-CONT       =  %x80-BF
;HCOLON          =  *WSP ":" SWS
;LHEX            =  DIGIT / %x61-66    ;lowercase a-f
;STAR            =  SWS "*" SWS        ; asterisk
;SLASH           =  SWS "/" SWS        ; slash
;EQUAL           =  SWS "=" SWS        ; equal
;LPAREN          =  SWS "(" SWS        ; left parenthesis
;RPAREN          =  SWS ")" SWS        ; right parenthesis
;RAQUOT          =  ">" SWS            ; right angle quote
;LAQUOT          =  SWS "<"            ; left angle quote
;COMMA           =  SWS "," SWS        ; comma
;SEMI            =  SWS ";" SWS        ; semicolon
;COLON           =  SWS ":" SWS        ; colon
;LDQUOT          =  SWS DQUOTE         ; open double quotation mark
;RDQUOT          =  DQUOTE SWS         ; close double quotation mark
comment         =  u_LPAREN *(ctext / quoted-pair / comment) u_RPAREN
ctext           =  %x21-27 / %x2A-5B / %x5D-7E / UTF8-NONASCII / u_LWS
quoted-string   =  e_SWS %x22 q-string %x22
q-string        =  *(qdtext / quoted-pair )
qdtext          =  u_LWS / %x21 / %x23-5B / %x5D-7E / UTF8-NONASCII
quoted-pair     =  "\" (%x00-09 / %x0B-0C / %x0E-7F)
;LWS             =  [*WSP CRLF] 1*WSP  ; linear whitespace (one or more line continue or space)
;SWS             =  [u_LWS]              ; sep (optional) whitespace (zero or more line continue or space)
WSP             =  %x20 / %x09

; B.1.  Core Rules ; RFC 4234 ABNF October 2005
ALPHA          =  %x41-5A / %x61-7A   ; A-Z / a-z
CHAR           =  %x01-7F ; any 7-bit US-ASCII character, ;  excluding NUL
CR             =  %x0D ; carriage return
CRLF           =  (CR LF) / LF / CR; modified to be forgiving on line ender
DIGIT          =  %x30-39 ; 0-9
;DQUOTE         =  %x22 ; " (Double Quote)
HEXDIG         =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
LF             =  %x0A ; linefeed
OCTET          =  %x00-FF ; 8 bits of data
SP             =  %x20
